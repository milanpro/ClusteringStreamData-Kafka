plugins {
    id 'idea'
    id 'base'
}

group 'de.hpi.msd'
version '1.0-SNAPSHOT'

task npmInstall(type: Exec) {
    inputs.file("package.json")
    commandLine "npm", "install"
}

configure(npmInstall) {
    group = 'install'
    description = 'Executes npm install'
}

task npmBuild(type: Exec) {
    dependsOn npmInstall
    inputs.file("package.json")
    inputs.dir("src")
    outputs.dir("build")

    commandLine "npm", "run", "build"
}

configure(npmBuild) {
    group = 'build'
    description = 'Executes npm run build'
}

task distJar(type: Jar) {
    dependsOn npmBuild
    from('build') {
        into 'public'
    }
}

configure(distJar) {
    group = 'distribution'
    description = 'Builds a jar containing all frontend Files'

    archiveBaseName = 'WebServerFrontend'
    destinationDirectory = file("${projectDir}/build_out")
}

assemble.dependsOn(distJar)

artifacts {
    archives distJar
}

clean {
    delete distJar.destinationDirectory
}